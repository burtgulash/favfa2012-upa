\documentclass[titlepage]{article}
\usepackage[utf8]{inputenc}
\usepackage[czech]{babel}
\usepackage{listings}
\usepackage{mips}

\include{pygments}

\begin{document}
\lstset{language=[mips]Assembler,
		basicstyle=\small\sffamily,
		numbers=left,
		numberstyle=\tiny,
		frame=tb}
\begin{titlepage}
\begin{center}
	\mbox{} \\[3cm]
	\huge{2. Semestrální práce z předmětu KIV/ÚPA} \\[2.5cm]
	\Large{Tomáš Maršálek, A10B0632P} \\
	\large{marsalet@students.zcu.cz} \\[1cm]
	\normalsize{\today}
\end{center}
\thispagestyle{empty}
\end{titlepage}

\section{Zadání}
Dělení 16 bitů / 16 bitů = 16 bitů + 16 bitů (výsledek + zbytek) (bez použití
instrukce dělení). Vstupy a výstupy hexadecimálně.

\section{Řešení}
Na dělení je použit klasický algoritmus dělení (long division). Ve vyšším
jazyce odpovídá následujícímu kódu:
\input{div.c}

Vstupy a výstupy používají pomocný buffer, ze kterého jsou Hornerovým schématem
rozkódovány, respektive zakódovány.

Oproti řešení z KIV/POT je zde dělení na 32 bitech namísto 16.

\section{Uživatelská příručka}
Uživatel je vyzván na zadání dvou hexadecimálních čísel, dělence a dělitele. Po
zadání je uživateli představen výsledek.  Výsledkem je bezznamínkový podíl a
zbytek. Pozn. hexadecimální číslice musí být velkými písmeny (tzn. 12ABC3, ne
12abc3) a maximální délka může čísla může být 8 hexadecimálních znaků (32
bitů). Vstup není nijak kontrolován, je třeba zadat číslo přesně bez jakýkoliv
přebytečných znaků.

\section{Pseudoinstrukce}
Pseudoinstrukce použité v programu jsou ve výsledku přeloženy do
strojových instrukcí. Např.:
\\[.3cm]

\begin{center}
\begin{minipage}[b]{0.45\linewidth}
Pseudoinstrukce
\begin{verbatim}
move	$v0, $0
b		endif
bltu	$s1, $a1, L4

\end{verbatim}
\end{minipage}
\begin{minipage}[b]{0.45\linewidth}
Strojová instrukce
\begin{verbatim}
addi	$v0, $0, 0
beq		$0, $0, endif
sltu	$1, $s1, $a1
bne		$1, $0, L4
\end{verbatim}
\end{minipage}
\end{center}

\section{Zpožděné čtení z paměti}
Instrukce čtení z paměti $la$, $lw$ a $li$ proběhnou zpožděně. Za poslední
instrkcí čtení (tj. $li$) je vložena prázdná instrukce, aby nedošlo k datovému
hazardu.

\begin{verbatim}
110 la		$a0, buf
111 lw		$a1, len
112 li		$v0, 8
113 nop
\end{verbatim}

\section{Datový hazard}
Jedná se o hazard typu RAW (Read after Write), protože registr \$t0 je použit v
instrukci $addu$, která je závislá na předchozí $move$ instrukci.
\begin{verbatim}
40	move	$t0, $a2
41	addu 	$v0, $v0, $t0
\end{verbatim}


\section{Listing programu}
\begin{lstlisting}
    .data

newline:    .asciiz       "\n"
delenec:    .asciiz       "delenec: "
delitel:    .asciiz       "delitel: "
podil:      .asciiz       "podil: "
zbytek:     .asciiz       "zbytek: "
buf:        .space        33
len:        .word         33

    .text

# int _from_hex(void *buf as $a0)
_from_hex:
        move    $v0, $0
cond:   lbu     $t0, 0($a0)
        nop
        beq     $t0, $0, endloop
        nop
        beq     $t0, 10, endloop
        nop
        bgeu    $t0, 'A', bigger
        nop
        subu    $t0, $t0, '0'
        b       endif
        nop
bigger: subu    $t0, $t0, 'A'-10
endif:  sll     $v0, $v0, 4
        addu    $v0, $v0, $t0
        addu    $a0, $a0, 1
        b       cond
        nop
endloop:jr      $ra
        nop


# void _to_hex(int n as $a0, void *buf as $a1, int len as $a2)
_to_hex:
        move    $v0, $a1
        move    $t0, $a2            # DATOVY HAZARD
        addu    $v0, $v0, $t0       # DATOVY HAZARD
        subu    $v0, $v0, 1
        sb      $0, ($v0)
loop:   subu    $v0, $v0, 1
        and     $t1, $a0, 15
        bltu    $t1, 10, small
        nop
        addu    $t1, 'A'-10
        b       store
        nop
small:  addu    $t1, '0'
store:  sb      $t1, ($v0)
        srl     $a0, $a0, 4
        bgtu    $a0, $0, loop
        nop
        jr      $ra
        nop


_div:
        subu    $sp, $sp, 8
        sw      $s0, 0($sp)
        sw      $s1, 4($sp)
        # -----------------

        # a = $a0
        # b = $a1
        # q = $s0
        # x = $s1

        li      $s0, 0             # q = 0
        move    $s1, $a1           # x = b

        srl     $t0, $a0, 1        # a >> 1
L1:     bgtu    $s1, $t0, L2       # if (x > (a >> 1)) goto L2
        nop
        sll     $s1, $s1, 1        # x <<= 1
        b       L1                 # goto L1
        nop
L2:     bltu    $s1, $a1, L4       # if (x < b) goto L4
        nop
        bltu    $a0, $s1, L3       # if (a < x) goto L3
        nop
        or      $s0, $s0, 1        # q |= 1
        subu    $a0, $a0, $s1      # a -= x
L3:     srl     $s1, $s1, 1        # x >>= 1
        sll     $s0, $s0, 1        # q <<= 1
        b       L2                 # goto L2
        nop
L4:     srl     $s0, $s0, 1        # q >>= 1
        move    $v0, $s0           # quotient = q
        move    $v1, $a0           # remainder = a

    
        # -----------------
        lw      $s0, 0($sp)
        lw      $s1, 4($sp)
        addu    $sp, $sp, 8 

        jr      $ra
        nop
    

main:
        # get dividend
        la      $a0, delenec
        li      $v0, 4
        nop
        syscall
        la      $a0, buf            # ZPOZDENI CTENI Z PAMETI
        lw      $a1, len            # ZPOZDENI CTENI Z PAMETI
        li      $v0, 8              # ZPOZDENI CTENI Z PAMETI
        nop                         # ZPOZDENI CTENI Z PAMETI
        syscall
        jal     _from_hex
        nop
        move    $s0, $v0
        

        # get divisor
        la      $a0, delitel
        li      $v0, 4
        nop
        syscall
        la      $a0, buf
        lw      $a1, len
        li      $v0, 8
        nop
        syscall
        jal     _from_hex
        nop
        move    $s1, $v0

        # call divide
        move    $a0, $s0
        move    $a1, $s1
        jal     _div
        nop
        move    $s0, $v0    # quotient
        move    $s1, $v1    # remainder


        # print hex quotient
        la      $a0, podil
        li      $v0, 4
        nop
        syscall
        move    $a0, $s0
        la      $a1, buf
        lw      $a2, len
        jal     _to_hex
        nop
        move    $a0, $v0
        li      $v0, 4
        nop
        syscall
        la      $a0, newline
        li      $v0, 4
        nop
        syscall

        # print hex remainder
        la      $a0, zbytek
        li      $v0, 4
        nop
        syscall
        move    $a0, $s1
        la      $a1, buf
        lw      $a2, len
        jal     _to_hex
        nop
        move    $a0, $v0
        li      $v0, 4
        nop
        syscall
        la      $a0, newline
        li      $v0, 4
        nop
        syscall

        # EXIT
        li      $v0, 10
        nop
        syscall
\end{lstlisting}
\section{Závěr}
Program byl vyvíjen na platformě GNU/Linux a testován pomocí simulátoru QtSpim.

\end{document}
